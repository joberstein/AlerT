plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 30
    testBuildType "staging"
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.jesseoberstein.alert"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.jesseoberstein.alert.config.InstrumentationTestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true",
                        "room.schemaLocation": "app/src/debug/schemas"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        staging {
            initWith debug
            applicationIdSuffix ".staging"
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']//, 'src/staging/assets']
            java.srcDirs = ['src/main/java', 'src/androidTest/java', 'src/staging/java']
        }

        test {
            assets.srcDirs = []
            java.srcDirs = ['src/main/java', 'src/test/java']
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    buildFeatures {
        dataBinding true
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    def room_version = "2.3.0"
    def lifecycle_version = "2.3.1"
    def paging_version = "3.0.0"
    def lombok_version = "1.18.20"
    def mockito_version = "3.10.0"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'

    // Lombok
    compileOnly "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"

    // Volley
    implementation 'com.android.volley:volley:1.2.0'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.4'

    // JSON Api
    implementation 'com.github.jasminb:jsonapi-converter:0.8'

    // Android Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.paging:paging-runtime:$paging_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    annotationProcessor "com.google.dagger:hilt-compiler:$hilt_version"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:27.0.0')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'

    ////////////////////////////////////////////////////////////////////////
    // Test Dependencies
    ////////////////////////////////////////////////////////////////////////
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:$mockito_version"

    // Room
    testImplementation "androidx.room:room-testing:$room_version"

    // Lombok
    testCompileOnly "org.projectlombok:lombok:$lombok_version"
    testAnnotationProcessor "org.projectlombok:lombok:$lombok_version"

    ////////////////////////////////////////////////////////////////////////
    // AndroidTest Dependencies
    ////////////////////////////////////////////////////////////////////////
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'junit:junit:4.13.2'

    // UI Testing
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Mockito
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    // Hilt
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Hamcrest Matchers
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'

    // Lombok
    androidTestCompileOnly "org.projectlombok:lombok:$lombok_version"
    androidTestAnnotationProcessor "org.projectlombok:lombok:$lombok_version"
}